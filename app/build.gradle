apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    sourceSets {
        main {
            //assets.srcDirs = ['assets']
            //jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        applicationId "com.ganxin.codebase"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true // dex突破65536的限制
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"] //// 默认是umeng的渠道
    }

    lintOptions {
        abortOnError false  //屏蔽lint检查
    }

    //Java编译环境
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental = true;
        javaMaxHeapSize "4g"
        preDexLibraries = false
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("/codebase_20161222.jks")
            storePassword "20161222"
            keyAlias "ganxin"
            keyPassword "20161222"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //versionNameSuffix "-debug"
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false" // 关闭Log
            minifyEnabled true   // 开启混淆
            zipAlignEnabled true // 开启zipalign优化
            shrinkResources true // 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro' // 混淆文件的位置
            signingConfig signingConfigs.release //签名配置信息

            //自定义输出文件名
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为codebase_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "codebase_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        xiaomi {}
        _360mobile {}
        wandoujia {}
        baidu {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

//针对dex包方法数超过5500,则进行拆分
afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--set-max-idx-number=55000"
// optional
//        dx.additionalParameters += "--main-dex-list=$projectDir/maindexlist.txt".toString() // enable the main-dex-list
//        dx.additionalParameters += "--minimal-main-dex" // enable the main-dex-list
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:multidex:1.0.0'
}
